import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'idea'
    id 'groovy-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'org.groovymc.modsdotgroovy'
base.archivesName = 'modsdotgroovy'

dependencies {
    implementation gradleApi()
    implementation localGroovy() // due to Gradle limitations, we must use localGroovy() for Gradle plugins

    implementation projects.core

    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'org.yaml:snakeyaml:2.1'
}

gradlePlugin {
    website = vcsUrl = 'https://github.com/GroovyMC/ModsDotGroovy'
    plugins {
        modsdotgroovy {
            id = 'org.groovymc.modsdotgroovy'
            displayName = 'ModsDotGroovy'
            description = 'A Gradle plugin for creation of mods.toml file from a groovy file'
            implementationClass = 'org.groovymc.modsdotgroovy.gradle.ModsDotGroovyGradlePlugin'
            tags.set(['groovy', 'minecraft', 'forge', 'neoforge', 'fabric', 'quilt', 'spigot'])
        }
    }
}

publishing {
    publications.register('gradlePlugin', MavenPublication) {
        artifactId = 'modsdotgroovy'
        version = project.version

        artifacts = [jar, sourcesJar]

        pom {
            name = 'mods.groovy Gradle plugin'
            url = 'https://github.com/GroovyMC/ModsDotGroovy'

            PomUtils.setGitHubDetails(pom, 'GroovyMC', 'ModsDotGroovy')

            licenses {
                license PomUtils.Licenses.MIT
            }
        }
    }
}

signing {
    sign publishing.publications.gradlePlugin
}
