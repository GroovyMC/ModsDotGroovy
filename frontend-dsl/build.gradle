plugins {
    id 'groovy'
    id 'maven-publish'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    group = 'org.groovymc.modsdotgroovy.frontend-dsl'

    java {
        withSourcesJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.apache.groovy:groovy:4.0.15'
        implementation projects.core
        compileOnly 'org.jetbrains:annotations:24.0.1'
    }

    tasks.named('groovydoc', Groovydoc).configure {
        use = true
    }

    tasks.withType(GroovyCompile).configureEach {
        groovyOptions.optimizationOptions.indy = true
        groovyOptions.optimizationOptions.groovyDoc = true
        groovyOptions.optimizationOptions.runtimeGroovydoc = true
        options.incremental = true
    }

    tasks.register('groovydocJar', Jar).configure {
        archiveClassifier.set('javadoc')
        from groovydoc.destinationDir
        dependsOn 'groovydoc'
    }

    publishing {
        repositories {
            maven {
                name = 'ModdingInquisitionMavenRepo'
                url = 'https://maven.moddinginquisition.org/snapshots'
                credentials {
                    username = findProperty('inquisitionMavenUser') ?: ''
                    password = findProperty('inquisitionMavenPassword') ?: ''
                }
            }
        }
    }
}

tasks.named('jar', Jar).configure {
    enabled = false
}
