import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

buildscript {
    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:3.0.11'
    }
}

plugins {
    id 'idea'
    id 'groovy'
    id 'signing'
    id 'maven-publish'
    id 'groovy-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group = project.hasProperty('inquisitionPublish') ? 'io.github.groovymc.modsdotgroovy' : 'org.groovymc.modsdotgroovy'
archivesBaseName = 'modsdotgroovy'

repositories {
    mavenCentral()
}

sourceSets {
    lib
}

license {
    header = project.file('LICENSE-header.txt')
}

dependencies {
    implementation gradleApi()
    libCompileOnly 'org.codehaus.groovy:groovy:3.0.11'
    libCompileOnly 'org.codehaus.groovy:groovy-json:3.0.11'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
}

pluginBundle {
    tags = ['groovy', 'forge', 'quilt', 'fabric', 'minecraft']
    website = 'https://github.com/GroovyMC/ModsDotGroovy'
    vcsUrl = 'https://github.com/GroovyMC/ModsDotGroovy'
}

gradlePlugin {
    plugins {
        modsdotgroovy {
            id = project.hasProperty('inquisitionPublish') ? 'io.github.groovymc.modsdotgroovy' : 'org.groovymc.modsdotgroovy'
            displayName = 'ModsDotGroovy'
            description = 'A Gradle plugin for creation of mods.toml file from a groovy file'
            implementationClass = 'io.github.groovymc.modsdotgroovy.ModsDotGroovy'
        }
    }
}

tasks.named('jar', Jar) {
    it.manifest.attributes makeAttributes()
}

final Map<String, ?> makeAttributes() {
    final var actualDateTime = OffsetDateTime.now(ZoneOffset.UTC).withNano(0)
    final var currentDateTime = DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(actualDateTime)
    return [
            'Specification-Title'     : archivesBaseName,
            'Specification-Vendor'    : 'GroovyMC',
            'Specification-Version'   : '1',
            'Implementation-Title'    : archivesBaseName,
            'Implementation-Version'  : "${project.version}",
            'Implementation-Vendor'   : 'Matyrobbrt',
            'Implementation-Timestamp': currentDateTime,
            'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
    ]
}

tasks.register('dslJar', Jar) {
    group = 'build'
    from sourceSets.lib.output
    archiveFileName = "dsl-${project.dsl_version}.jar"
    it.manifest.attributes makeAttributes() + ['Maven-Artifact' : "${project.group}:dsl:${project.version}"]
}

tasks.register('dslSources', Jar) {
    from sourceSets.lib.allSource
    archiveFileName = "dsl-${project.dsl_version}-sources.jar"
    classifier 'sources'
    it.manifest.attributes makeAttributes()
}
groovydoc {
    use = true
    setSource(sourceSets.lib.allSource)

    docTitle = "ModsDotGroovy v${version} - DSL"
}

tasks.create('groovydocJar', Jar) {
    classifier 'javadoc'
    from groovydoc.destinationDir
    dependsOn(groovydoc)
    archiveFileName = "dsl-${project.dsl_version}-javadoc.jar"
    classifier 'javadoc'
}

tasks.withType(GroovyCompile).configureEach {
    groovyOptions.encoding = 'UTF-8'
    groovyOptions.optimizationOptions.indy = true
}

publishing {
    publications {
        register('mavenDsl', MavenPublication) {
            groupId = project.hasProperty('inquisitionPublish') ? 'io.github.groovymc.modsdotgroovy' : 'org.groovymc.modsdotgroovy'
            artifactId = 'dsl'
            version = project.dsl_version

            artifacts = [dslJar, dslSources, groovydocJar]

            pom {
                name = "ModsDotGroovy DSL"
                description = "The ModsDotGroovy DSL"
                url = "https://github.com/Groovymc/modsdotgroovy"
                licenses {
                    license {
                        name = "MIT"
                    }
                }
                developers {
                    developer {
                        id = "groovymc"
                        name = "GroovyMC"
                        email = "holdings@groovymc.org"
                    }
                }
                scm {
                    connection = "https://github.com/Groovymc/modsdotgroovy.git"
                    url = "https://github.com/Groovymc/modsdotgroovy"
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ModdingInquisitionMavenRepo'
            url = 'https://maven.moddinginquisition.org/releases'
            credentials {
                username = (System.getenv('MAVEN_USER') ?: findProperty('inquisitionMavenUser')) ?: ''
                password = (System.getenv('MAVEN_PASSWORD') ?: findProperty('inquisitionMavenPassword')) ?: ''
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(System.getenv('SONATYPE_USER') ?: '')
            password.set(System.getenv('SONATYPE_PASSWORD') ?: '')
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        }
    }
}

if ('publishToMavenLocal' !in gradle.startParameter.taskNames) {
    signing {
        final signingKey = System.getenv('SIGNING_KEY') ?: ''
        final signingPassword = System.getenv('SIGNING_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenDsl
    }
}

project.tasks.register('configureTeamCityPlugin') {
    it.setOnlyIf {
        System.getenv('TEAMCITY_VERSION')
    }
    // Print the marker lines into the log which configure the pipeline
    it.doLast {
        project.getLogger().lifecycle('Setting project variables and parameters.')
        println "##teamcity[buildNumber 'P: ${project.version}']"
    }
}

project.tasks.register('configureTeamCityDSL') {
    it.setOnlyIf {
        System.getenv('TEAMCITY_VERSION')
    }
    // Print the marker lines into the log which configure the pipeline
    it.doLast {
        project.getLogger().lifecycle('Setting project variables and parameters.')
        println "##teamcity[buildNumber 'DSL: ${project.dsl_version}']"
    }
}

if (System.getenv('GPP_KEY')) {
    ext['gradle.publish.key'] = System.getenv('GPP_KEY')
    ext['gradle.publish.secret'] = System.getenv('GPP_SECRET')
}
