plugins {
    id 'groovy'
    id 'signing'
    id 'maven-publish'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    group = 'org.groovymc.modsdotgroovy.stock-plugins'

    java {
        withSourcesJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.apache.groovy:groovy:4.0.15'
        implementation projects.core
        compileOnly 'org.jetbrains:annotations:24.0.1'
    }

    tasks.named('groovydoc', Groovydoc).configure {
        use = true
    }

    tasks.withType(GroovyCompile).configureEach {
        groovyOptions.optimizationOptions.indy = true
        groovyOptions.optimizationOptions.groovyDoc = true
        groovyOptions.optimizationOptions.runtimeGroovydoc = true
        options.incremental = true
    }

    tasks.register('groovydocJar', Jar).configure {
        archiveClassifier = 'javadoc'
        from groovydoc.destinationDir
        dependsOn 'groovydoc'
    }

    publishing {
        repositories {
            maven {
                name = 'ModdingInquisitionMavenRepo'
                url = 'https://maven.moddinginquisition.org/snapshots'
                credentials {
                    username = System.getenv('MAVEN_USER') ?: findProperty('inquisitionMavenUser') ?: ''
                    password = System.getenv('MAVEN_PASSWORD') ?: findProperty('inquisitionMavenPassword') ?: ''
                }
            }
        }
    }

    signing {
        final String signingKey = System.getenv('SIGNING_KEY') ?: findProperty('groovyMCSigningKey') ?: ''
        final String signingPassword = System.getenv('SIGNING_PASSWORD') ?: findProperty('groovyMCSigningPassword') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
}

tasks.named('jar', Jar).configure {
    enabled = false
}
