import net.minecraftforge.gradleutils.PomUtils

tasks.named('groovydoc', Groovydoc).configure {
    source = sourceSets.main.allSource
    docTitle = "ModsDotGroovy v${project.version} - Forge plugin"
}

publishing {
    publications.register('stockPluginForge', MavenPublication) {
        artifactId = 'forge'
        version = project.version

        artifacts = [jar, sourcesJar, groovydocJar]

        pom {
            name = 'mods.groovy stock plugin for Forge'
            description = 'The stock plugin for mods.groovy on Forge'
            url = 'https://github.com/GroovyMC/ModsDotGroovy'

            PomUtils.setGitHubDetails(pom, 'GroovyMC', 'ModsDotGroovy')

            licenses {
                license PomUtils.Licenses.MIT
            }
        }
    }
}

signing {
    sign publishing.publications.stockPluginForge
}

dependencies {
    api projects.stockPlugins.forgelike
}
